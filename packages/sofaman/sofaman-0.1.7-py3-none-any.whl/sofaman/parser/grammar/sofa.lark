
%declare _INDENT _DEDENT
%import common (CNAME, WS_INLINE, WS)
%ignore WS_INLINE

?start: sofa
sofa: (imports | actors | components | classes | interfaces | relations | stereotypes | primitives | diagrams | capabilities | domains | packages | _NL)+

# Terminals -----

COMMENT     : /#.*/ _NL*
_COLON      : ":"
_NL         : /((\r+|\n+|(\r\n)+)(\t|\s)*)/
_PIPE       : "|"
_COMMA      : ","
_LBRA       : "["
_RBRA       : "]"
_QUOTE      : "\""
_AT         : "@"

STRING          : /(?!\[).+/
SIMPLE_STRING   : ("a".."z"|"A".."Z"|"0".."9"|" "|"_")+
INTEGER         : ("0".."9")+
DECIMAL         : INTEGER? "." INTEGER
NUMBER          : INTEGER | DECIMAL
QUOTED_STRING   : _QUOTE STRING _QUOTE

%ignore COMMENT

# Struct Rules ------

name: CNAME | QUOTED_STRING
QNAME: SIMPLE_STRING ("." SIMPLE_STRING)*
scalar: STRING

_string_newline: STRING _NL?
multiline_scalar: _string_newline+

_vector_item: (SIMPLE_STRING | QUOTED_STRING | QNAME) (_COMMA|_NL)?
vector_items: _vector_item+
vector: _LBRA vector_items _RBRA

key_value: name _COLON scalar _NL?
vector_value: name _COLON vector _NL?
multiline_value: name _COLON _PIPE _NL _INDENT multiline_scalar _DEDENT
multiline_vector_value: name _COLON multiline_vector_body

properties: _INDENT (vector_value | multiline_vector_value | key_value | map | multiline_value)+ _DEDENT
map: name _COLON _NL properties

multiline_vector_body: _NL _INDENT ("-" (map | name) _NL?)+ _DEDENT

# Element Rules ----

struct_body: _COLON _NL properties

# ---
actor: "actor" struct
actors: actor+

# ---
association: "associates"
bidirectional_association: "bi-associates"
inheritance: "inherits"
information_flow: "flow"
bidirectional_inflow: "bi-flow"
realization: "implements"
aggregation: "aggregates"
composition: "composes"

port: _AT (CNAME|NUMBER)
relation_name: name port?

relation_type: (association | bidirectional_association | bidirectional_inflow | inheritance | information_flow | realization | aggregation | composition)

relation: "relation" relation_name relation_type relation_name struct_body? _NL*
relations: relation+

# ---

stereotype: "stereotype" name _COLON vector _NL*
stereotypes: stereotype+

# ---

diagram: "diagrams" _COLON (vector | multiline_vector_body) _NL*
diagrams: diagram+

# ---

style: "style"
import: "import" style? name _NL*
imports: import+

# --- Type defs ---

param_list: "(" vector_items* ")"
struct: QNAME param_list? struct_body? _NL*

# ---

primitive: "primitives" _COLON vector _NL*
primitives: primitive+

# ---
component: "component" struct
components: component+

# ---

clazz: "class" struct
classes: clazz+

# ---

interface: "interface" struct
interfaces: interface+

# ---

package: "package" struct
packages: package+

# ---

capability: "capability" struct
capabilities: capability+

# ---

domain: "domain" struct
domains: domain+
